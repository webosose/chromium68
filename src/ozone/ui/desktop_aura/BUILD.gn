# Copyright (c) 2018 LG Electronics, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

platform_list_txt_file = "$root_gen_dir/ui/ozone/platform_list.txt"
desktop_factory_ozone_list_cc_file = "$target_gen_dir/desktop_factory_ozone_list.cc"

source_set("views_ozone_wayland") {
  sources = [
    "desktop_drag_drop_client_wayland.cc",
    "desktop_drag_drop_client_wayland.h",
    "desktop_factory_ozone_stubs.cc",
    "desktop_factory_ozone_stubs.h",
    "desktop_factory_ozone_wayland.cc",
    "desktop_factory_ozone_wayland.h",
    "desktop_platform_screen.h",
    "desktop_screen_wayland.cc",
    "desktop_screen_wayland.h",
    "desktop_window_tree_host_ozone_wayland.cc",
    "desktop_window_tree_host_ozone_wayland.h",
    "ozone_util.cc",
    "ozone_util.h",
    desktop_factory_ozone_list_cc_file,
  ]

  defines = [ "VIEWS_IMPLEMENTATION" ]

  deps = [
    "//skia",
    "//ui/accessibility:ax_enums_mojo",
    "//ui/views:features",
    ":generate_desktop_factory_ozone_list",
  ]

}

action("generate_desktop_factory_ozone_list") {
  script = "//ui/ozone/generate_constructor_list.py"

  inputs = [
    platform_list_txt_file,
  ]
  outputs = [
    desktop_factory_ozone_list_cc_file,
  ]

  args = [
    "--platform_list=" + rebase_path(platform_list_txt_file, root_build_dir),
    "--output_cc=" + rebase_path(desktop_factory_ozone_list_cc_file, root_build_dir),
    "--namespace=views",
    "--typename=DesktopFactoryOzone",
    "--using=views::DesktopFactoryOzone",
    "--include=\"ui/views/widget/desktop_aura/desktop_factory_ozone.h\"",
  ]

  deps = [
    "//ui/ozone:generate_ozone_platform_list",
  ]
}
